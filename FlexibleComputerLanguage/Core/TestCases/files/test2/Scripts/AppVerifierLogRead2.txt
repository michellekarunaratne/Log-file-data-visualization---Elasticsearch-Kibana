// Read the Application Verifer log file
$X.ReadFromFile(D:MurtazaCode/FlexibleComputerLanguage/Core/TestCasesfiles/test2/AppVerifierLog)

// First get the xml def line out
	

// Now there should be two childs for X
// First one is the xml definition line. Second one is the rest of the text
$X.Children:=FLC
$FLC.SeekToBegin
$FLC.Seek(#1)
$FLC.GetCurrentElement:=INFO
$INFO.GetValue:=VAL
$VAL.TrimLeft
$VAL.TrimRight
$INFO.SetValue($VAL)
$INFO.Expand({<avrf:logfile,</avrf:logfile>,<avrf:logSession,</avrf:logSession>})
$INFO.Children:=SLC
$SLC.SeekToBegin
$SLC.GetCurrentElement:=ELEM

While($ELEM.IsNotNull)
	If($ELEM.GetLValue.IsStringEqualTo(<avrf:logSession).And($ELEM.GetRValue.IsStringEqualTo(</avrf:logSession>)))
		$ELEM.GetValue.TrimLeft
		$ELEM.GetValue.TrimRight
		$ELEM.Expand({<avrf:logEntry,</avrf:logEntry>})
		$ELEM.Children:=TLC
		$TLC.SeekToBegin
		$TLC.GetCurrentElement:=SESSION
		$TLC.Seek(#1)
		$TLC.GetCurrentElement:=ELEM2
		While($ELEM2.IsNotNull)
			IfNot($ELEM2.GetLValue.IsStringEqualTo(<avrf:logEntry).And($ELEM2.GetRValue.IsStringEqualTo(</avrf:logEntry>)))
				$TLC.Seek(#1)
				$TLC.GetCurrentElement:=ELEM2
				Continue
			EndIf
			$Y.AddNode:=LOGENTRY
			$ELEM2.Expand({<avrf:message>,</avrf:message>,<avrf:formatmessage>,</avrf:formatmessage>,__
				<avrf:parameter,</avrf:parameter,<avrf:stackTrace>,</avrf:stackTrace>})
			$ELEM2.Children:=FOLC
			$FOLC.SeekToBegin
			$FOLC.GetCurrentElement:=ELEM3
			$ELEM3.GetValue:=VAL
			$VAL.TrimLeft
			$VAL.TrimRight
			$ELEM3.SetValue($VAL)
			$LOGENTRY.AddNode:=LOGENTRYHEADER
			$LOGENTRYHEADER.SetType(#100)
			$LOGENTRYHEADER.SetValue($VAL)
			$ELEM3.Expand({Time=",LayerName=",StopCode=",Severity=","})
			$ELEM3.Children:=LEHC
			$LEHC.SeekToBegin
			$LEHC.GetCurrentElement:=ELEM4
			While($ELEM4.IsNotNull)
				If($ELEM4.GetLValue.IsStringEqualTo(Time=").And($ELEM4.GetRValue.IsStringEqualTo(")))
					$LOGENTRYHEADER.AddNode:=HEADERCHILD
					$HEADERCHILD.SetType(#50)
					$HEADERCHILD.SetValue($ELEM4.GetValue)
				EndIf

				If($ELEM4.GetLValue.IsStringEqualTo(LayerName=").And($ELEM4.GetRValue.IsStringEqualTo(")))
					$LOGENTRYHEADER.AddNode:=HEADERCHILD
					$HEADERCHILD.SetType(#51)
					$HEADERCHILD.SetValue($ELEM4.GetValue)
				EndIf

				If($ELEM4.GetLValue.IsStringEqualTo(StopCode=").And($ELEM4.GetRValue.IsStringEqualTo(")))
					$LOGENTRYHEADER.AddNode:=HEADERCHILD
					$HEADERCHILD.SetType(#52)
					$HEADERCHILD.SetValue($ELEM4.GetValue)
				EndIf

				If($ELEM4.GetLValue.IsStringEqualTo(Severity=").And($ELEM4.GetRValue.IsStringEqualTo(")))
					$LOGENTRYHEADER.AddNode:=HEADERCHILD
					$HEADERCHILD.SetType(#53)
					$HEADERCHILD.SetValue($ELEM4.GetValue)
				EndIf

				$LEHC.Seek(#1)
				$LEHC.GetCurrentElement:=ELEM4
			Do
			$FOLC.Seek(#1)
			$FOLC.GetCurrentElement:=ELEM3
			While($ELEM3.IsNotNull)
				If($ELEM3.GetLValue.IsStringEqualTo(<avrf:message>).And($ELEM3.GetRValue.IsStringEqualTo(</avrf:message>)))
					$LOGENTRY.AddNode:=MESSAGE
					$MESSAGE.SetValue($ELEM3.GetValue)
					$MESSAGE.SetType(#101)
				EndIf

				If($ELEM3.GetLValue.IsStringEqualTo(<avrf:formatmessage>).And($ELEM3.GetRValue.IsStringEqualTo(</avrf:formatmessage>)))
					$LOGENTRY.AddNode:=FORMATMESSAGE
					$FORMATMESSAGE.SetValue($ELEM3.GetValue)
					$FORMATMESSAGE.SetType(#102)
				EndIf

				If($ELEM3.GetLValue.IsStringEqualTo(<avrf:parameter).And($ELEM3.GetRValue.IsStringEqualTo(</avrf:parameter)))
					$LOGENTRY.AddNode:=PARAM
					$ELEM3.Expand({>})
					$ELEM3.Children:=FILC
					$FILC.SeekToBegin
					$PARAM.SetCustomString($FILC.GetCurrentElement.GetValue)
					$FILC.Seek(#1)
					$PARAM.SetValue($FILC.GetCurrentElement.GetValue)
					$PARAM.SetType(#103)
				EndIf

				$FOLC.Seek(#1)
				$FOLC.GetCurrentElement:=ELEM3
			Do

			$TLC.Seek(#1)
			$TLC.GetCurrentElement:=ELEM2
		Do
	EndIf

	$SLC.Seek(#1)
	$SLC.GetCurrentElement:=ELEM	
Do

// Done constructing Y

// Start extracting data from Y and generating the report in Z
$Y.GetSubtree:=YTREE
$YTREE.IsType(#53):=SEVERITY
$SEVERITY.IsValue(Error):=ERRORS
$SEVERITY.IsValue(Warning):=WARNINGS
$Z.AddNode:=ERRORNODE
$Z.AddNode:=WARNINGNODE
$ERRORS.GetItemCount.ToString:=STR
$STR.AddPostFix( errors@NEWLINE)
$ERRORNODE.SetValue($STR)
$WARNINGS.GetItemCount.ToString:=STR
$STR.AddPrefix(@NEWLINE)
$STR.AddPostFix( warnings@NEWLINE)
$WARNINGNODE.SetValue($STR)
$ERRORS.Parent.GetChildOfType(#51):=LAYERNAMES
$LAYERNAMES.SeekToBegin
$LAYERNAMES.GetCurrentElement:=ELEM
While($ELEM.IsNotNull)
	$ERRORNODE.Children:=ENC
	$ENC.SeekToBegin
	$ENC.GetCurrentElement:=ELEM2
	// Create a boolean variable with a initial vaule of TRUE. Following is a hack
	$ERRORNODE.IsNotNull:=NEWLAYER
	While($ELEM2.IsNotNull)
		If($ELEM2.GetCustomString.IsStringEqualTo($ELEM.GetValue))
			$ELEM2.GetWeight:=WEIGHT
			$WEIGHT.Add(#1)
			$ELEM2.SetWeight($WEIGHT)
			$ERRORNODE.IsNull:=NEWLAYER
			$ELEM2:=LAYERNODE
			Break
		EndIf

		$ENC.Seek(#1)
		$ENC.GetCurrentElement:=ELEM2
	Do

	If($NEWLAYER)
		$ERRORNODE.AddNode:=LAYERNODE
		$LAYERNODE.SetWeight(#1)
		$LAYERNODE.SetCustomString($ELEM.GetValue)
	EndIf

	If($LAYERNODE.IsNotNull)
		// Add new children containing error information
		// Time
		$ELEM.Parent.GetChildOfType(#50):=YNODE
		$LAYERNODE.AddNode:=TIMENODE
		$TIMENODE.SetValue($YNODE.GetValue)
		$TIMENODE.GetValue:=VAL
		$VAL.AddPrefix(@NEWLINETime : )
		$VAL.AddPostFix(@NEWLINE)
		$TIMENODE.SetValue($VAL)

		// Stop code
		$ELEM.Parent.GetChildOfType(#52):=YNODE
		$LAYERNODE.AddNode:=STOPCODENODE
		$STOPCODENODE.SetValue($YNODE.GetValue)
		$STOPCODENODE.GetValue:=VAL
		$VAL.AddPrefix(Stop Code : )
		$VAL.AddPostFix(@NEWLINE)
		$STOPCODENODE.SetValue($VAL)

		// Message
		$ELEM.Parent.Parent.GetChildOfType(#101):=YNODE
		$LAYERNODE.AddNode:=MESSAGENODE
		$MESSAGENODE.SetValue($YNODE.GetValue)
		$MESSAGENODE.GetValue:=VAL
		$VAL.AddPrefix(Message : )
		$VAL.AddPostFix(@NEWLINE)
		$MESSAGENODE.SetValue($VAL)

		// Format message
		$ELEM.Parent.Parent.GetChildOfType(#102):=YNODE
		$LAYERNODE.AddNode:=FMESSAGENODE
		$FMESSAGENODE.SetValue($YNODE.GetValue)
		$FMESSAGENODE.GetValue:=VAL
		$VAL.AddPrefix(Format Message : )
		$VAL.AddPostFix(@NEWLINE)
		$FMESSAGENODE.SetValue($VAL)
	EndIf

	$LAYERNAMES.Seek(#1)
	$LAYERNAMES.GetCurrentElement:=ELEM
Do

// Add value strings for layer nodes
$ERRORNODE.Children:=ENC
$ENC.SeekToBegin
$ENC.GetCurrentElement:=ELEM2
While($ELEM2.IsNotNull)
	$ELEM2.SetValue($ELEM2.GetCustomString)
	$ELEM2.GetValue:=VAL
	$VAL.AddPrefix(@NEWLINE)
	$VAL.AddPostFix( : )
	$VAL.AddPostFix($ELEM2.GetWeight.ToString)
	$VAL.AddPostFix(@NEWLINE======================================================================@NEWLINE)
	$ELEM2.SetValue($VAL)

	$ENC.Seek(#1)
	$ENC.GetCurrentElement:=ELEM2
Do

$WARNINGS.Parent.GetChildOfType(#51):=LAYERNAMES
$LAYERNAMES.SeekToBegin
$LAYERNAMES.GetCurrentElement:=ELEM
While($ELEM.IsNotNull)
	$WARNINGNODE.Children:=ENC
	$ENC.SeekToBegin
	$ENC.GetCurrentElement:=ELEM2
	// Create a boolean variable with a initial vaule of TRUE. Following is a hack
	$WARNINGNODE.IsNotNull:=NEWLAYER
	While($ELEM2.IsNotNull)
		If($ELEM2.GetCustomString.IsStringEqualTo($ELEM.GetValue))
			$ELEM2.GetWeight:=WEIGHT
			$WEIGHT.Add(#1)
			$ELEM2.SetWeight($WEIGHT)
			$WARNINGNODE.IsNull:=NEWLAYER
			$ELEM2:=LAYERNODE
			Break
		EndIf

		$ENC.Seek(#1)
		$ENC.GetCurrentElement:=ELEM2
	Do

	If($NEWLAYER)
		$WARNINGNODE.AddNode:=LAYERNODE
		$LAYERNODE.SetWeight(#1)
		$LAYERNODE.SetCustomString($ELEM.GetValue)
	EndIf

	If($LAYERNODE.IsNotNull)
		// Add new children containing error information
		// Time
		$ELEM.Parent.GetChildOfType(#50):=YNODE
		$LAYERNODE.AddNode:=TIMENODE
		$TIMENODE.SetValue($YNODE.GetValue)
		$TIMENODE.GetValue:=VAL
		$VAL.AddPrefix(@NEWLINETime : )
		$VAL.AddPostFix(@NEWLINE)
		$TIMENODE.SetValue($VAL)

		// Stop code
		$ELEM.Parent.GetChildOfType(#52):=YNODE
		$LAYERNODE.AddNode:=STOPCODENODE
		$STOPCODENODE.SetValue($YNODE.GetValue)
		$STOPCODENODE.GetValue:=VAL
		$VAL.AddPrefix(Stop Code : )
		$VAL.AddPostFix(@NEWLINE)
		$STOPCODENODE.SetValue($VAL)

		// Message
		$ELEM.Parent.Parent.GetChildOfType(#101):=YNODE
		$LAYERNODE.AddNode:=MESSAGENODE
		$MESSAGENODE.SetValue($YNODE.GetValue)
		$MESSAGENODE.GetValue:=VAL
		$VAL.AddPrefix(Message : )
		$VAL.AddPostFix(@NEWLINE)
		$MESSAGENODE.SetValue($VAL)

		// Format message
		$ELEM.Parent.Parent.GetChildOfType(#102):=YNODE
		$LAYERNODE.AddNode:=FMESSAGENODE
		$FMESSAGENODE.SetValue($YNODE.GetValue)
		$FMESSAGENODE.GetValue:=VAL
		$VAL.AddPrefix(Format Message : )
		$VAL.AddPostFix(@NEWLINE)
		$FMESSAGENODE.SetValue($VAL)
	EndIf

	$LAYERNAMES.Seek(#1)
	$LAYERNAMES.GetCurrentElement:=ELEM
Do

// Add value strings for layer nodes
$WARNINGNODE.Children:=ENC
$ENC.SeekToBegin
$ENC.GetCurrentElement:=ELEM2
While($ELEM2.IsNotNull)
	$ELEM2.SetValue($ELEM2.GetCustomString)
	$ELEM2.GetValue:=VAL
	$VAL.AddPrefix(@NEWLINE)
	$VAL.AddPostFix( : )
	$VAL.AddPostFix($ELEM2.GetWeight.ToString)
	$VAL.AddPostFix(@NEWLINE======================================================================@NEWLINE)
	$ELEM2.SetValue($VAL)

	$ENC.Seek(#1)
	$ENC.GetCurrentElement:=ELEM2
Do

$Z.GetAggregatedValue.WriteToFile(D:/MurtazaCode/FlexibleComputerLanguage/Core/TestCases/files/test2/Output/report)